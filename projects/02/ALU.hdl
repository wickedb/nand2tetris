// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:  
    //because these ops happen sequentially, their outputs feed into each other 
    
    //1. zero the x input 
    Mux16(a=x, b=false, sel=zx, out=xout);
    
    //2. negate the x input
    Not16(in=xout, out=xnot);
    Mux16(a=xout, b=xnot, sel=nx, out=negoutx);
    
    //3. zero the y input
    Mux16(a=y, b=false, sel=zy, out=yout);
    
    //4. negate the y input
    Not16(in=yout, out=ynot);
    Mux16(a=yout, b=ynot, sel=ny, out=negouty);
    
    //5. x&y or x+y 
    And16(a=negoutx, b=negouty, out=andxy);
    Add16(a=negoutx, b=negouty, out=addxy);
    //choose based on f
    Mux16(a=andxy, b=addxy, sel=f, out=xy);
    
    //6. negate output 
    Not16(in=xy, out=notxy);
    //choose based on no + Produce main output here + find sign bit for ng
    Mux16(a=xy, b=notxy, sel=no, out=out, out[15]=ng, out[0..7]=slice1, out[8..15]=slice2);
    
    //7. ^ng is signbit out[15]
    //8. find if out == 0
    Or8Way(in=slice1, out=iszero1);
    Or8Way(in=slice2, out=iszero2);
    Or(a=iszero1, b=iszero2, out=iszero);
    Not(in=iszero, out=zr);
}
